apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    //签名
    signingConfigs {
        debug {
            v1SigningEnabled true
            v2SigningEnabled true
            try {
                def signP = getSigningProperties(true)

                storeFile file(signP[0])
                storePassword signP[1]
                keyAlias signP[2]
                keyPassword signP[3]
            } catch (ex) {
                ex.printStackTrace()
            }
        }
        release {
            v1SigningEnabled true
            v2SigningEnabled true
            try {
                def signP = getSigningProperties(false)

                storeFile file(signP[0])
                storePassword signP[1]
                keyAlias signP[2]
                keyPassword signP[3]
            } catch (ex) {
                ex.printStackTrace()
            }
        }
    }
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    //会导致无法联网
//    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //配置阿里 ARouter 路由
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        // Specifies the ABI configurations of your native
        // libraries Gradle should build and package with your APK.
//        ndk {
//            abiFilters "armeabi"
//        }
    }
    buildTypes {
        //正式模式
        release {
            //正式模式
            buildConfigField "boolean", "IS_DEBUG", "false"
            //正式版构建时间
            buildConfigField "String", "buildTime", "\"" + getRealTime() + "\""

            //是否开启混淆
            minifyEnabled false
            //使用正式版签名配置签名
            signingConfig signingConfigs.release
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        //调试模式
        debug {
            //调试模式
            buildConfigField "boolean", "IS_DEBUG", "true"
            //调试版构建时间
            buildConfigField "String", "buildTime", "\"" + getRealTime() + "\""

            //是否开启混淆，开启之后不能debug
            minifyEnabled false
            //使用调试版签名配置签名
            signingConfig signingConfigs.debug
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    //开启DataBinding (注意每个用到DataBinding的Module都需要打开)
    dataBinding {
        enabled true
    }
    //开启DataBinding (注意每个用到DataBinding的Module都需要打开) (用于 gradle tool 4.0 以上)
    buildFeatures{
        dataBinding = true
    }
    //开启Java8
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    //支持 ViewHolder 中使用 Kotlin 的 findViewById 简写
    androidExtensions {
        experimental = true
    }
    //避免项目中存在相同的jar包，导致运行失败
    dexOptions {
        preDexLibraries = false
    }
    //出现错误的原因是：AndroidStudio严格审查png图片，就是png没有达到AndroidStudio的要求
    //注：最重要的就是.9图要严格要求做好。禁止Gradle检查PNG的合法性
    aaptOptions.cruncherEnabled = false
    aaptOptions.useNewCruncher = false

    //lint配置信息，这里主要是设置自定义lint检测后报告路径，LintOptions类型
    //命令行：gradlew lint      注意，studio自带lint检查，setting——> Inspection
//    lintOptions {
//        println("开始执行lint检测代码逻辑")
//        // 设置为 true，则当 Lint 发现错误时停止 Gradle 构建
//        //移除lint检测的error
//        abortOnError false
//        // 设置为 true，则当有错误时会显示文件的全路径或绝对路径 (默认情况下为true)
//        absolutePaths true
//        // 仅检查指定的问题（根据 id 指定）
//        check 'NewApi', 'InlinedApi'
//        // 设置为 true 则检查所有的问题，包括默认不检查问题
//        checkAllWarnings true
//        // 设置为 true 后，release 构建都会以 Fatal 的设置来运行 Lint。
//        // 如果构建时发现了致命（Fatal）的问题，会中止构建（具体由 abortOnError 控制）
//        checkReleaseBuilds true
//        // 不检查指定的问题（根据问题 id 指定）
//        disable 'TypographyFractions','TypographyQuotes'
//        // 检查指定的问题（根据 id 指定）
//        enable 'RtlHardcoded','RtlCompat', 'RtlEnabled'
//        // 在报告中是否返回对应的 Lint 说明
//        explainIssues true
//        // 写入报告的路径，默认为构建目录下的 lint-results.html
//        //htmlOutput file("lint-report.html")
//        // 设置为 true 则会生成一个 HTML 格式的报告
//        htmlReport true
//        // 设置为 true 则只报告错误
//        ignoreWarnings true
//        // 重新指定 Lint 规则配置文件
//        //lintConfig file("default-lint.xml")
//        // 设置为 true 则错误报告中不包括源代码的行号
//        noLines true
//        // 设置为 true 时 Lint 将不报告分析的进度
//        quiet true
//        // 设置为 true 则显示一个问题所在的所有地方，而不会截短列表
//        showAll true
//        // 配置写入输出结果的位置，格式可以是文件或 stdout
//        textOutput 'stdout'
//        // 设置为 true，则生成纯文本报告（默认为 false）
//        textReport false
//        // 设置为 true，则会把所有警告视为错误处理
//        warningsAsErrors true
//        // 写入检查报告的文件（不指定默认为 lint-results.xml）
//        //xmlOutput file("lint-report.xml")
//        // 设置为 true 则会生成一个 XML 报告
//        xmlReport false
//        // 将指定问题（根据 id 指定）的严重级别（severity）设置为 Fatal
//        fatal 'NewApi', 'InlineApi'
//        // 将指定问题（根据 id 指定）的严重级别（severity）设置为 HttpException
//        error 'Wakelock', 'TextViewEdits'
//        // 将指定问题（根据 id 指定）的严重级别（severity）设置为 Warning
//        warning 'ResourceAsColor'
//        // 将指定问题（根据 id 指定）的严重级别（severity）设置为 ignore
//        ignore 'TypographyQuotes'
//    }

    //android studio默认so文件加载目录为:src/main/jniLibs
    //如在module的build.gradle按照如下方式,自定义了so文件加载目录请确保对应目录下只有armeabi目录
    sourceSets {
        main{
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation project(path: ':base')
    //ARouter 路由框架 注解执行器
    kapt rootProject.ext.annotationProcessor["arouter"]
}

//获取实时时间
static def getRealTime() {
    return new Date().format("MMddHHmmss", TimeZone.getTimeZone("Asia/Beijing"))
}

//读取项目中的keystore.properties签名文件，安全
static String[] getSigningProperties(boolean isDebug) {
    File propFile = new File('keystore.properties')
    if (propFile.exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(propFile))
        if (isDebug) {
            def signP = new String[4]
            signP[0] = props['DEBUG_STORE_FILE']
            signP[1] = props['STORE_PASSWORD']
            signP[2] = props['KEY_ALIAS']
            signP[3] = props['KEY_PASSWORD']
            return signP
        } else {
            def signP = new String[4]
            signP[0] = props['STORE_FILE']
            signP[1] = props['STORE_PASSWORD']
            signP[2] = props['KEY_ALIAS']
            signP[3] = props['KEY_PASSWORD']
            return signP
        }
    }
}
